import com.github.spotbugs.SpotBugsTask

plugins {
	id 'org.springframework.boot' version '2.1.3.RELEASE'
	id 'com.jfrog.artifactory' version '4.8.1'
	id 'maven-publish'
	id 'com.github.spotbugs' version '1.6.5'
	id 'jacoco'
	id 'java'
	id 'org.ajoberstar.grgit' version '3.0.0'
}

apply plugin: 'io.spring.dependency-management'

group = 'uk.gov.ons.census.fwmt'
version = '1.0.5-SNAPSHOT'
sourceCompatibility = '11'
 
repositories {
	mavenLocal()
	if (!project.hasProperty('omitSDCArtifactory')) {
		maven { url 'http://artifactory-sdc.onsdigital.uk/artifactory/libs-release' }
		maven { url 'http://artifactory-sdc.onsdigital.uk/artifactory/libs-snapshot' }
	}
	mavenCentral()
}

springBoot {
	buildInfo {
		properties {
			additional = [
					'Git-Commit': grgit.head().getAbbreviatedId(8)
			]
		}
	}
}

dependencies {
	compile('uk.gov.ons.census.fwmt:census-fwmt-canonical:1.0.11-RELEASE')
	compile('uk.gov.ons.census.fwmt:census-fwmt-common:1.0.15-RELEASE')
	compile('uk.gov.ons.census.fwmt:census-fwmt-events:1.0.6-RELEASE')
	
	compile('org.projectlombok:lombok')
	annotationProcessor('org.projectlombok:lombok')
	testAnnotationProcessor('org.projectlombok:lombok')

	compile('org.springframework.retry:spring-retry')
	compile('org.springframework:spring-aop')

	compile('org.aspectj:aspectjweaver')

	compile('org.springframework.boot:spring-boot-starter')
	compile('org.springframework.boot:spring-boot-starter-amqp')
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('org.springframework.boot:spring-boot-starter-actuator')
	compile('org.springframework.boot:spring-boot-starter-security')
	compileOnly('org.projectlombok:lombok')
	testImplementation('org.springframework.boot:spring-boot-starter-test')

	compile('io.springfox:springfox-swagger2:2.9.2')
	compile('io.springfox:springfox-swagger-ui:2.9.2')
}

artifactory {
	contextUrl = System.getenv('ARTIFACTORY_URL')
	publish {
		repository {
			repoKey = 'libs-snapshot-local'
			username = System.getenv('ARTIFACTORY_USERNAME')
			password = System.getenv('ARTIFACTORY_PASSWORD')
		}
	}
}

publishing {
	publications {
		maven(MavenPublication) {
			groupId = group
			artifactId = 'census-fwmt-outcome-service'
			version = version

			from components.java
		}
	}
}

artifactoryPublish {
	publications('maven')
}

jacocoTestReport {
	reports {
		xml.enabled = true
		html.enabled = false
	}
}
test.finalizedBy(project.tasks.jacocoTestReport)

check.dependsOn jacocoTestReport

spotbugs {
	toolVersion = '3.1.9'
}

tasks.withType(SpotBugsTask) {
	reports {
		xml.enabled = false
		html.enabled = true
	}
}
