import com.github.spotbugs.SpotBugsTask

plugins {
	id 'org.springframework.boot' version '2.1.3.RELEASE'
	id 'com.jfrog.artifactory' version '4.8.1'
	id 'maven-publish'
	id 'com.github.spotbugs' version '1.6.5'
	id 'jacoco'
	id 'java'
}

apply plugin: 'io.spring.dependency-management'

group = 'uk.gov.ons.fwmt'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
	mavenLocal()
	if (!project.hasProperty('omitSDCArtifactory')) {
		maven { url 'http://artifactory-sdc.onsdigital.uk/artifactory/libs-release' }
		maven { url 'http://artifactory-sdc.onsdigital.uk/artifactory/libs-snapshot' }
	}
	mavenCentral()
}

dependencies {
	compile("uk.gov.ons.fwmt:fwmt-census-canonical:1.0.2-SNAPSHOT")
	compile("uk.gov.ons.fwmt:fwmt-census-common:1.0.2-SNAPSHOT")
	compile("uk.gov.ons.fwmt:fwmt-census-events:1.0.0-SNAPSHOT")

	compile('org.projectlombok:lombok')
	annotationProcessor('org.projectlombok:lombok')
	testAnnotationProcessor('org.projectlombok:lombok')

	compile('org.springframework.retry:spring-retry')
	compile('org.springframework:spring-aop')

	compile('org.aspectj:aspectjweaver')
	
	compile('org.springframework.boot:spring-boot-starter')
	compile( 'org.springframework.boot:spring-boot-starter-amqp')
	compile( 'org.springframework.boot:spring-boot-starter-web')
	compile('org.springframework.boot:spring-boot-starter-actuator')
	compile('org.springframework.boot:spring-boot-starter-security')
	compileOnly 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

artifactory {
	contextUrl = System.getenv('ARTIFACTORY_URL')
	publish {
		repository {
			repoKey = 'libs-snapshot-local'
			username = System.getenv('ARTIFACTORY_USERNAME')
			password = System.getenv('ARTIFACTORY_PASSWORD')
		}
	}
}

publishing {
	publications {
		maven(MavenPublication) {
			groupId = group
			artifactId = 'fwmt-census-feedback-service'
			version = version

			from components.java
		}
	}
}

artifactoryPublish {
	publications('maven')
}

jacocoTestReport {
	reports {
		xml.enabled = true
		html.enabled = false
	}
}
test.finalizedBy(project.tasks.jacocoTestReport)

check.dependsOn jacocoTestReport

spotbugs {
	toolVersion = '3.1.9'
}

tasks.withType(SpotBugsTask) {
	reports {
		xml.enabled = false
		html.enabled = true
	}
}

	
